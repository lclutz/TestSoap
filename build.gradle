plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group 'de.lclutz'
version '1.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/generated-sources/jaxb'
        }
    }
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.schema = "src/main/resources/countries.xsd"

    outputs.dir sourcesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
        }
    }
}

compileJava.dependsOn genJaxb

repositories {
    mavenCentral()
}

configurations {
    jaxb
}

bootJar {
    archiveBaseName.set('gs-producing-web-service')
    archiveVersion.set('0.1.0')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'wsdl4j:wsdl4j'

    jaxb  'javax.xml.bind:jaxb-api:2.3.1'
    jaxb  'com.sun.xml.bind:jaxb-xjc:2.3.3'
    jaxb  'com.sun.xml.bind:jaxb-impl:2.3.3'
    jaxb  'com.sun.xml.bind:jaxb-osgi:2.3.3'

    // https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
    compileOnly group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

    // https://mvnrepository.com/artifact/com.sun.xml.bind/jaxb-impl
    runtimeOnly group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}